buildscript {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}
apply plugin: 'maven-publish'
apply plugin: 'com.android.library'

ext {
    tools = [
            groupID: 'com.github.infomaniak',
            versionName: '1.9.5',
            artifactID: 'pdfiumandroid'
    ]
}

android {
    namespace 'com.shockwave.pdfium'
    
    compileSdk 34
    ndkVersion "26.1.10909125"

    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }

    packagingOptions {
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/x86_64/libc++_shared.so'
        pickFirst 'lib/x86/libc++_shared.so'

        pickFirst 'lib/armeabi-v7a/libjniPdfium.so'
        pickFirst 'lib/arm64-v8a/libjniPdfium.so'
        pickFirst 'lib/x86_64/libjniPdfium.so'
        pickFirst 'lib/x86/libjniPdfium.so'

        pickFirst 'lib/armeabi-v7a/libmodpdfium.so'
        pickFirst 'lib/arm64-v8a/libmodpdfium.so'
        pickFirst 'lib/x86_64/libmodpdfium.so'
        pickFirst 'lib/x86/libmodpdfium.so'
    }

    defaultConfig {
        minSdkVersion 21
        targetSdk 34
        versionCode 1
        versionName tools.versionName

        buildConfigField("String", "LIBRARY_VERSION_NAME", "\"${defaultConfig.versionName}\"")

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    buildFeatures {
        buildConfig = true
    }
}

repositories {
    google()
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.collection:collection:1.3.0'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.findByName('release')
                groupId = tools.groupID
                artifactId = tools.artifactID
                version = tools.versionName
            }
        }
    }
}
